import net.minecrell.pluginyml.bukkit.BukkitPluginDescription

plugins {
    id "java"
    id "io.freefair.lombok" version "8.11"
    id "io.github.goooler.shadow" version "8.1.8"
    id "net.minecrell.plugin-yml.bukkit" version "0.6.0" apply(true)
}

repositories {
    mavenCentral()

    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    maven { url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
}

dependencies {
    compileOnly "org.spigotmc:spigot-api:1.16.5-R0.1-SNAPSHOT"

    implementation "dev.dejvokep:boosted-yaml:1.3.7"
    implementation "io.github.revxrsal:lamp.bukkit:4.0.0-beta.19"
    implementation "io.github.revxrsal:lamp.common:4.0.0-beta.19"
    implementation "net.kyori:adventure-platform-bukkit:4.3.4"
    implementation "net.kyori:adventure-text-minimessage:4.17.0"
}

tasks.compileJava {
    options.compilerArgs.addAll(["-Xlint:-options", "-parameters"])
    options.encoding = "UTF-8"
}

tasks.jar.manifest {
    attributes.put("Implementation-Vendor", rootProject.findProperty("author").toString())
    attributes.put("Implementation-Version", rootProject.version)
}

tasks.register("build-plugin") {
    dependsOn tasks.clean, tasks.shadowJar
}

tasks.shadowJar {
    setArchiveFileName("${rootProject.name}-${rootProject.version}.jar")
    preserveFileTimestamps(false)
    exclude("META-INF/*/**")
    minimize()
}

bukkit {
    name = rootProject.name
    version = rootProject.version
    description = rootProject.description
    website = "aidencooper.github.io"

    main = rootProject.group.toString() + ".factionsremastered.FactionsPlugin"

    foliaSupported = false

    apiVersion = "1.13"

    load = BukkitPluginDescription.PluginLoadOrder.POSTWORLD
}

java.sourceCompatibility = JavaVersion.VERSION_1_8
java.targetCompatibility = JavaVersion.VERSION_1_8